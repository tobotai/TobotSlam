apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.tobot.map"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 80
        versionName "4.0.1"
        // dex突破65535的限制
        multiDexEnabled true
        // 自定义配置信息
        buildConfigField("String", "BASE_URL", "${BASE_URL}")
        buildConfigField("String", "VERSION_TXT_URL", "${VERSION_TXT_URL}")
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    buildTypes {
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true // 开启混淆
            zipAlignEnabled true // 压缩优化
            shrinkResources true // 移出无用资源
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            minifyEnabled true // 开启混淆
            zipAlignEnabled true // 压缩优化
            shrinkResources true // 移出无用资源
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            assets.srcDirs = ['assets', 'src/main/assets', 'src/main/assets/']
        }
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def APP_NAME = "TobotSlam"
            def createTime = new Date().format("YYYY-MM-dd", TimeZone.getTimeZone("GMT+08:00"))
            outputFileName = APP_NAME + "_v" + defaultConfig.versionName + "_" + createTime + ".apk"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    testImplementation 'junit:junit:4.13.2'
    implementation 'com.jakewharton:butterknife:9.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation files('libs/TobotNet-1.2.4.jar')
    implementation files('libs/TobotSlam-2.12.0.jar')
}
